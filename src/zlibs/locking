#!/usr/bin/env zsh
#
# Jaro Mail, your humble and faithful electronic postman
#
# a tool to easily and privately handle your e-mail communication
#
# Copyleft (C) 2010-2012 Denis Roio <jaromil@dyne.org>
#
# This source  code is free  software; you can redistribute  it and/or
# modify it under the terms of  the GNU Public License as published by
# the Free  Software Foundation; either  version 3 of the  License, or
# (at your option) any later version.
#
# This source code is distributed in  the hope that it will be useful,
# but  WITHOUT ANY  WARRANTY;  without even  the  implied warranty  of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# Please refer to the GNU Public License for more details.
#
# You should have received a copy of the GNU Public License along with
# this source code; if not, write to:
# Free Software Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

lock() {
    func "lock: $1"
    $WORKDIR/bin/dotlock ${1}
    case $? in
	1) error "Cannot lock non existing file: $1"
	    return 1 ;;
	3) error "Locked file in use: $1"

	    pidcheck ${1}
	    return 3 ;;

	5) error "Impossible to lock: $1"
	    return 5 ;;
	# success
	0) echo "$$" > ${1}.pid
	    return 0 ;;
    esac
}

newlock() { # create locked
    func "creating locked file: $1"
    touch $1
    chmod 600 $1
    lock $1
}

pidcheck() { # check if lock belongs to us
    if [ -r "${1}.pid" ]; then
	lockpid="`cat ${1}.pid`"
	func "lockpid: $lockpid"
	if [ "$$" = "$lockpid" ]; then
	    func "${1} lock belongs to us"
	else
	    error "Unlock attempt by multiple processes on `basename $1`"
	    { test "$FORCE" = "1" } || { _prun=1
		while [ "$_prun" = "1" ]; do
		    for p in `ps ax | awk '{ print $1 }'`; do
			{ test "$p" = "$lockpid" } && { break }
		    done
		    if [ "$p" = "$lockpid" ]; then
			act "Owner (${lockpid}) still running, waiting release..."
			sleep 1; continue
		    else
			act "Owner (pid ${_pid}) not running, taking over the lock"
			rm -f ${1}.pid; echo "$$" > ${1}.pid
			_prun=0
		    fi
		done
	    }
	    act "left behind by $lockpid - we ($$) will take over"
	fi
    else
	act "left behind by unknown owner, we'll take it over"
	rm -f ${1}.pid; echo "$$" > ${1}.pid
    fi
    return 0
}

unlock() {
    func "unlock: $1"

    pidcheck $1
    { test $? = 0 } || { return 1 }

    $WORKDIR/bin/dotlock -u ${=@}
    { test $? != 0 } && { error "Unable to unlock: $1"; return 1 }
    { test -r "${1}.pid" } && { rm -f ${1}.pid }
    return 0
}
unlink() { # delete a file that we are locking
    func "unlink: $1"
    # use with care! this can permanently erase currently locked files

    pidcheck $1
    { test $? = 0 } || { return 1 }

    # signal that is unlinking (for parallel operation)
    touch ${1}.unlink
    (
	${=rm} ${1}
	touch ${1}
	$WORKDIR/bin/dotlock -d -f ${1}
	{ test $? != 0 } && { error "Unable to unlink: $1"; return 1 }
	{ test -r "${1}.pid" } && { rm -f ${1}.pid }
	rm -f ${1}.unlink
    ) &!
    return 0
}
